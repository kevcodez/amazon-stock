buildscript {
    ext {
        kotlinVersion = '1.2.51'
        junit_version = '5.2.0'
        dokkaVersion  = '0.9.17'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
    }
}

plugins {
    // Use task dependencyUpdates to check for updates
    id "com.github.ben-manes.versions" version "0.20.0"
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

group 'de.kevcodez.amazon'
version '1.0.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile "org.jboss.resteasy:resteasy-client:3.6.0.Final"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            pom {
                name = 'Amazon Stock'
                description = 'Kotlin library to retrieve the current stock from an amazon article using Product Advertising API'
                url = 'https://github.com/kevcodez/amazon-stock'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/kevcodez/pubg-api-kotlin/blob/master/LICENS'
                    }
                }
                scm {
                    url = 'https://github.com/kevcodez/amazon-stock'
                    connection = 'scm:git:git://github.com/kevcodez/amazon-stock.git'
                    developerConnection = 'scm:git:ssh://git@github.com:kevcodez/amazon-stock.git'
                }
                developers {
                    developer {
                        id = 'kevcodez'
                        name = 'Kevin Gr√ºneberg'
                        email = 'k.grueneberg1994@gmail.com'
                    }
                }
            }


        }
    }

    def myNexusUsername = hasProperty('nexusUsername') ? nexusUsername : 'foo'
    def myNexusPassword = hasProperty('nexusPassword') ? nexusPassword : 'bar'

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username myNexusUsername
                password myNexusPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}